section .note.GNU-stack noalloc noexec nowrite progbits
section .data               
;Canviar Nom i Cognom per les vostres dades.
developer db "_Xavier_ _Sánchez_",0

;Constants que també estan definides en C.
DIMMATRIX    equ 9
SIZEMATRIX   equ 81

section .text            
;Variables definides en Assemblador.
global developer     
                         
;Subrutines d'assemblador que es criden des de C.
global countMinesP2, showNumMinesP2  , posCursorP2, showMarkP2, 
global moveCursorP2, markMineP2  , searchMinesP2, checkEndP2, playP2

;Funcions de C que es criden des de assemblador
extern clearScreen_C, gotoxyP2_C, getchP2_C, printchP2_C
extern printBoardP2_C, printMessageP2_C

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; ATENCIÓ: Recordeu que en assemblador les variables i els paràmetres 
;;   de tipus 'char' s'han d'assignar a registres de tipus  
;;   BYTE (1 byte): al, ah, bl, bh, cl, ch, dl, dh, sil, dil, ..., r15b
;;   les de tipus 'short' s'han d'assignar a registres de tipus 
;;   WORD (2 bytes): ax, bx, cx, dx, si, di, ...., r15w
;;   les de tipus 'int' s'han d'assignar a registres de tipus 
;;   DWORD (4 bytes): eax, ebx, ecx, edx, esi, edi, ...., r15d
;;   les de tipus 'long' s'han d'assignar a registres de tipus 
;;   QWORD (8 bytes): rax, rbx, rcx, rdx, rsi, rdi, ...., r15
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Les subrutines en assemblador que s'han de modificar per a
;; implementar el pas de paràmetres són:
;;   countMinesP2, showNumMinesP2  , posCursor, showMarkP2, 
;;   moveCursorP2, mineMarkerP2
;; La subrutina que s'ha de canviar la funcionalitat:
;;   checkEndP2
;; La subrutina que s'ha d'implementar:
;;   searchMinesP2
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Aquesta subrutina es dóna feta. NO LA PODEU MODIFICAR.
; Situar el cursor en una fila i una columna de la pantalla
; en funció de la fila (edi) i de la columna (esi) rebuts com 
; a paràmetre cridant a la funció gotoxyP2_C.
; 
; Variables globals utilitzades:	
; Cap
; 
; Paràmetres d'entrada : 
; (rowScreen): rdi(edi) : Fila de la pantalla on es situa el cursor.
; (colScreen): rsi(esi) : Columna de la pantalla on es situa el cursor.
;
; Paràmetres de sortida: 
; Cap
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
gotoxyP2:
   push rbp
   mov  rbp, rsp
   ;guardem l'estat dels registres del processador perquè
   ;les funcions de C no mantenen l'estat dels registres.
   push rax
   push rbx
   push rcx
   push rdx
   push rsi
   push rdi
   push r8
   push r9
   push r10
   push r11
   push r12
   push r13
   push r14
   push r15

   ; Quan cridem la funció gotoxyP2_C(int row_num, int row_num) des d'assemblador 
   ; el primer paràmetre (row_num) s'ha de passar pel registre rdi(edi), i
   ; el segon  paràmetre (col_num) s'ha de passar pel registre rsi(esi).	
   call gotoxyP2_C
 
   ;restaurar l'estat dels registres que s'han guardat a la pila.
   pop r15
   pop r14
   pop r13
   pop r12
   pop r11
   pop r10
   pop r9
   pop r8
   pop rdi
   pop rsi
   pop rdx
   pop rcx
   pop rbx
   pop rax

   mov rsp, rbp
   pop rbp
   ret


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Aquesta subrutina es dóna feta. NO LA PODEU MODIFICAR.
; Mostrar un caràcter (dil) a la pantalla, rebut com a paràmetre, 
; en la posició on està el cursor cridant la funció printchP2_C.
; 
; Variables globals utilitzades:	
; Cap
; 
; Paràmetres d'entrada : 
; (c) : rdi(dil) : Caràcter a mostrar.
; 
; Paràmetres de sortida: 
; Cap
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
printchP2:
   push rbp
   mov  rbp, rsp
   ;guardem l'estat dels registres del processador perquè
   ;les funcions de C no mantenen l'estat dels registres.
   push rax
   push rbx
   push rcx
   push rdx
   push rsi
   push rdi
   push r8
   push r9
   push r10
   push r11
   push r12
   push r13
   push r14
   push r15

   ; Quan cridem la funció printchP2_C(char c) des d'assemblador, 
   ; el paràmetre (c) s'ha de passar pel registre rdi(dil).
   call printchP2_C
 
   ;restaurar l'estat dels registres que s'han guardat a la pila.
   pop r15
   pop r14
   pop r13
   pop r12
   pop r11
   pop r10
   pop r9
   pop r8
   pop rdi
   pop rsi
   pop rdx
   pop rcx
   pop rbx
   pop rax

   mov rsp, rbp
   pop rbp
   ret
   

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Aquesta subrutina es dóna feta. NO LA PODEU MODIFICAR.
; Llegir una tecla i retornar el caràcter associat (al) sense 
; mostrar-lo per pantalla, cridant la funció getchP2_C.
; 
; Variables globals utilitzades:	
; Cap
; 
; Paràmetres d'entrada : 
; Cap
; 
; Paràmetres de sortida: 
; (c) : rax(al) : Caràcter llegit des del teclat.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
getchP2:
   push rbp
   mov  rbp, rsp
   ;guardem l'estat dels registres del processador perquè
   ;les funcions de C no mantenen l'estat dels registres.
   push rbx
   push rcx
   push rdx
   push rsi
   push rdi
   push r8
   push r9
   push r10
   push r11
   push r12
   push r13
   push r14
   push r15
   
   mov rax, 0
   ; Quan cridem la funció getchP2_C des d'assemblador, 
   ; retorna sobre el registre rax(al) el caràcter llegit
   call getchP2_C
 
   ;restaurar l'estat dels registres que s'han guardat a la pila.
   pop r15
   pop r14
   pop r13
   pop r12
   pop r11
   pop r10
   pop r9
   pop r8
   pop rdi
   pop rsi
   pop rdx
   pop rcx
   pop rbx
   
   mov rsp, rbp
   pop rbp
   ret 



;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Recórrer la matriu (mines), rebuda como a paràmetre, per comptar 
; el nombres de mines (numMines) y retornar el valor.
; 
; Variables globals utilitzades:	
; Cap.
; 
; Paràmetres d'entrada : 
; (mines)   :rdi(rdi): Adreça de la matriu on hi han les mines.
; 
; Paràmetres de sortida: 
; (numMines):rax(rax): Mines que queden per marcar.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
countMinesP2:
   push rbp
   mov  rbp, rsp

   ; comptador de mines = 0
   xor rax, rax

   ; i = 0 (fila)        
   xor rsi, rsi

outer_loop:
   cmp rsi, 9
   jge end_count

   xor rdx, rdx

inner_loop:
   ; final de fila?
   cmp rdx, 9
   jge next_row

   ; adreça mines[i][j]
   mov rcx, rsi
   imul rcx, 9 
   add rcx, rdx
   add rcx, rdi

   ; comparem amb '*'
   mov bl, byte [rcx]
   cmp bl, '*'
   jne no_mine

   ; mina trobada
   inc rax

no_mine:
   ; j++
   inc rdx             
   jmp inner_loop

next_row:
   ; i++
   inc rsi
   jmp outer_loop

end_count:
   ; totes les mines
   mov rsp, rbp
   pop rbp
   ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Converteix el valor (numMines), mines que queden per marcar,
; (valor entre 0 i 99) en dos caràcters ASCII. 
; Si (numMines) és més gran de 99 canviar el valor a 99.
; S'ha de dividir el valor (numMines) entre 10, el quocient representarà 
; les desenes (tens) i el residu les unitats (units), i després 
; s'han de convertir a ASCII sumant 48, caràcter '0'.
; Si les desenes (tens)==0  mostra un espai ' '.
; Mostra els dígits (caràcter ASCII) de les desenes a la fila 3, 
; columna 40 de la pantalla i les unitats a la fila 3, columna 41.
; Per a posicionar el cursor s'ha de cridar a la subrutina gotoxyP2 i 
; per a mostrar els caràcters a la subrutina printchP2.
; 
; Variables globals utilitzades:	
; Cap
; 
; Paràmetres d'entrada : 
; (numMines) :rdi(rdi): Mines que queden per marcar.
; 
; Paràmetres de sortida: 
; Cap
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
showNumMinesP2:
   push rbp
   mov  rbp, rsp
   
   ; si numMines > 99 llavors numMines = 99;
   cmp rdi, 99
   jle snm_ok
   mov rdi, 99

snm_ok:
   ; faig el calcul de la variable 'tens'
   mov rax, rdi      ; rax = numMines
   mov rbx, 10
   xor rdx, rdx      ; netejar per DIV
   div rbx           ; rax = tens, rdx = units

   ; mostrar desenes
   ; si (tens > 0) ASCII = tens + '0' en cas contrari ' '
   cmp rax, 0
   jne snm_tens_digit

   mov bl, ' '
   jmp snm_tens_print

snm_tens_digit:
   ; canvi a ASCII
   add al, '0'
   mov bl, al

snm_tens_print:
   ; crido a gotoxyP2_C(3,40)
   mov edi, 3
   mov esi, 40
   call gotoxyP2

   ; printchP2_C(charac)
   mov dil, bl
   call printchP2

   ; mostro unitats
   mov al, dl        ; units en AL
   add al, '0'       ; convertir a ASCII
   mov bl, al

   ; crido a gotoxyP2_C(3,41)
   mov edi, 3
   mov esi, 41
   call gotoxyP2

   ; crido a printchP2_C(charac)
   mov dil, bl
   call printchP2

snm_end:
   mov rsp, rbp
   pop rbp
   ret	

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Posicionar el cursor a la pantalla dins del tauler, en funció de
; la fila (row) i la columna(col), rebuts com a paràmetre,
; posició del cursor dins del tauler.
; Per a calcular la posició del cursor a pantalla utilitzar 
; aquestes fórmules:
; rowScreen=(row*2)+8
; colScreen=(col*4)+7
; Per a posicionar el cursor es cridar a la subrutina gotoxyP2.
; 
; Variables globals utilitzades:	
; Cap
; 
; Paràmetres d'entrada : 
; (row) :rdi(edi): Fila on està el cursor dins les matrius mines i marks.
; (col) :rsi(esi): Columna on està el cursor dins les matrius mines i marks.
; 
; Paràmetres de sortida: 
; Cap 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
posCursorP2:
   push rbp
   mov  rbp, rsp
   ;guardem l'estat dels registres del processador perquè
   ;les funcions de C no mantenen l'estat dels registres.

   ; params => rdi = row i rsi = col
   
   ; rowScreen = row * 2 + 8
   mov eax, edi        ; eax = row
   imul eax, 2         ; eax = row * 2
   add eax, 8          ; eax = rowScreen
   mov edi, eax        ; edi = rowScreen

   ; colScreen = col * 4 + 7
   mov eax, esi        ; eax = col
   imul eax, 4         ; eax = col * 4
   add eax, 7          ; eax = colScreen
   mov esi, eax        ; esi = colScreen

   ; crido a gotoxyP2(rowScreen, colScreen)
   call gotoxyP2
   
   pc_end:
   ;restaurar l'estat dels registres que s'han guardat a la pila.
   
   mov rsp, rbp
   pop rbp
   ret


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Mostrar el caràcter de la matriu (marks) a la posicionar del cursor de
; la fila (row) i la columna (col), rebuts com a paràmetre, a la pantalla.
; Per a posicionar el cursor s'ha de cridar a la subrutina posCursorP2.
; i per a mostrar el caràcter a la subrutina printchP2.

; Variables globals utilitzades:	
; Cap
; 
; Paràmetres d'entrada : 
; (marks):rdi(rdi): Adreça de la matriu amb les mines marcades i les mines de les caselles obertes.
; (row)  :rsi(esi): Fila on està el cursor dins les matrius mines i marks.
; (col)  :rdx(edx): Columna on està el cursor dins les matrius mines i marks.
; 
; Paràmetres de sortida: 
; Cap.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 
showMarkP2:
   push rbp
   mov  rbp, rsp
   ;guardem l'estat dels registres del processador perquè
   ;les funcions de C no mantenen l'estat dels registres.  
   
   ; params => rdi = &marks[0][0], rsi = row i rdx = col
   mov r8d, esi 
   mov r9d, edx

   ; posicionar el cursor
   mov edi, esi
   mov esi, edx
   call posCursorP2

   ; accedeixo a a marks[row][col]
   mov eax, r8d
   imul eax, eax, 9
   add eax, r9d
   
   ; rdi = base address de marks[]
   add rdi, rax

   ; carregar el caràcter
   mov bl, byte [rdi]

   ; mostro el caràcter vía printchP2(c)
   mov dil, bl
   call printchP2

smk_end:
   ;restaurar l'estat dels registres que s'han guardat a la pila.
   mov rsp, rbp
   pop rbp
   ret


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;		
; Actualitzar la posició del cursor al tauler, que està a la fila (row)
; i la columna (col), en funció de la tecla premuda (c),
; rebuts com a parametre.
; Si es surt fora del tauler no actualitzar la posició del cursor.
; ( i:amunt, j:esquerra, k:avall, l:dreta)
; Amunt i avall: ( row--/++ ) 
; Esquerra i Dreta: ( col--/++ )
; Retornar l'index que indica la posició del cursor dins la matriu
; amb la següent fórmula: index = row*DIMMATRIX+col
; No s'ha de posicionar el cursor a la pantalla.
;  
; Variables globals utilitzades:	
; Cap   
; 
; Paràmetres d'entrada : 
; (c)     :rdi(dil): Caràcter llegit de teclat.
; (row)   :rsi(esi): Fila on està el cursor dins les matrius mines i marks.
; (col)   :rdx(edx): Columna on està el cursor dins les matrius mines i marks.
; 
; Paràmetres de sortida: 
; (index) :rax(eax): Índex que indica la posició del cursor dins la matriu.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 
moveCursorP2:
   push rbp
   mov  rbp, rsp
   ;guardem l'estat dels registres del processador perquè
   ;les funcions de C no mantenen l'estat dels registres.
   
   ; params => rdi = c, rsi = row i rdx = col
   ; switch(c)
   cmp dil, 'i'
   je mv_amunt

   cmp dil, 'j'
   je mv_esquerra

   cmp dil, 'k'
   je mv_avall

   cmp dil, 'l'
   je mv_dreta

   ;altres tecles no mou
   jmp mv_calcul_index 

; i = amunt
mv_amunt:
   ; if (row > 0)
   cmp esi, 0
   jle mv_calcul_index
   
   ; row--
   dec esi
   jmp mv_calcul_index

; j = esquerra
mv_esquerra:
   ; if (col > 0)
   cmp edx, 0
   jle mv_calcul_index
   
   ; col--
   dec edx
   jmp mv_calcul_index

; k = avall
mv_avall:
   ; if (row < DIMMATRIX-1)
   cmp esi, 8
   jge mv_calcul_index

   ; row++   
   inc esi
   jmp mv_calcul_index

; l = dreta
mv_dreta:
   ; if (col < DIMMATRIX-1)
   cmp edx, 8
   jge mv_calcul_index

   ; col++
   inc edx
   jmp mv_calcul_index

mv_calcul_index:
   ; eax = row
   mov eax, esi
   
   ; eax = row * 9
   imul eax, eax, 9

   ; eax += col
   add eax, edx

mc_end:
   ;restaurar l'estat dels registres que s'han guardat a la pila.
   mov rsp, rbp
   pop rbp
   ret


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 
; Marcar/desmarcar una mina a la matriu (marks) a la posició actual
; del cursor indicada per les variables (row) fila i (col) columna,
; rebuts com a paràmetre
; Si en aquella posició de la matriu (marks) hi ha un espai en blanc i 
; no hem marcat totes les mines, marquem una mina posant una 'M' a la 
; matriu (marks) i decrementem el nombre de mines que queden per 
; marcar (numMines), si en aquella posició de la matriu (marks) hi ha 
; una 'M', posarem un espai (' ') a la matriu (marks) i incrementem 
; el nombre de mines que queden per marcar (numMines).
; Si hi ha un altre valor no canviarem res.
; Retornar el nombre de mines (numMines) que queden per marcar actualitzat .
; Mostrar el canvi fet a la matriu (marks) cridant la subrutina showMarkP2.
; 
; Variables globals utilitzades:	
; Cap
; 
; Paràmetres d'entrada : 
; (marks)   :rdi(rdi): Adreça de la matriu amb les mines marcades i les mines de les caselles obertes.
; (row)     :rsi(esi): Fila on està el cursor dins les matrius mines i marks.
; (col)     :rdx(edx): Columna on està el cursor dins les matrius mines i marks.
; (numMines):rcx(rcx): Mines que queden per marcar.
; 
; Paràmetres de sortida: 
; (numMines) : rax(ax) : Mines que queden per marcar.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;  
markMineP2:
   push rbp
   mov  rbp, rsp
   ;guardem l'estat dels registres del processador perquè
   ;les funcions de C no mantenen l'estat dels registres.
   
   ; els paràmtres són rdi = &marks, rsi = row, rdx = col, rcx = numMines

   ;guardo marks
   mov r8, rdi

   ; calculo row * 9 + col
   ; eax = row
   mov eax, esi
   ; eax = row * 9
   imul eax, eax, 9
   ; eax = row * 9 + col
   add eax, edx
   ; rdi = &marks[row][col]
   add rdi, rax

   ;assigno valor a marks
   mov bl, byte[rdi]

   ; if (marks[row][col] == ' ' && numMines > 0)
   cmp bl, ' '
   jne mm_else_block

   ; numMines > 0 ?
   cmp rcx, 0
   jle mm_else_block

   ; marquem 'M' si presiono tecla
   mov byte [rdi], 'M'
   ; numMines--
   dec rcx                   
   jmp mm_mostrar_marca

mm_else_block:
   ; else if (marks[row][col] == 'M')
   cmp bl, 'M'
   jne mm_mostrar_marca

   ; desmarquem si es espai blanc
   mov byte [rdi], ' '
   ; numMines++
   inc rcx

mm_mostrar_marca:
   mov rdi, r8 
   call showMarkP2

mm_end:
   ;restaurar l'estat dels registres que s'han guardat a la pila.    
   
   mov rsp, rbp
   pop rbp
   ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;  
; Obrir casella. Mirar quantes mines hi ha al voltant de la 
; posició actual del cursor a la fila (row) i la columna (col)
; de la matriu (mines), rebuts com a paràmetre.
; Si a la posició actual de la matriu (marks) hi ha un espai (' ') 
;   Mirar si a la matriu (mines) hi ha una mina ('*').
;   Si hi ha una mina canviar l'estat (state) a 3 "Explosió".
;	 Sinó, comptar quantes mines hi ha al voltant de la posició 
;     actual i actualitzar la posició de la matriu (marks) amb 
;     el nombre de mines (caràcter ASCII del valor), per fer-ho, cal 
;     sumar 48 ('0') al valor.
; Mostrar el canvi fet a la matriu (marks) cridant la subrutina showMarkP2.
; Si no hi ha un espai, vol dir que hi ha una mina marcada ('M') o la 
; casella ja s'ha obert (hi ha el nombre de mines que ja s'ha calculat 
; anteriorment), no fer res.
; Retornar l'estat del joc actualitzat (state).
;  
; Variables globals utilitzades:	
; Cap
; 
; Paràmetres d'entrada :
; (marks):rdi(rdi) : Adreça de la matriu amb les mines marcades i les mines de les caselles obertes.
; (row)  :rsi(esi): Fila on està el cursor dins les matrius mines i marks.
; (col)  :rdx(edx): Columna on està el cursor dins les matrius mines i marks.
; (mines):rcx(rcx): Adreça de la matriu amb les mines.
; (state):r8 (r8w): Estat del joc. 
; 
; Paràmetres de sortida: 
; (state):rax(ax) : Estat del joc.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;  
searchMinesP2:
   push rbp
   mov  rbp, rsp
   ;guardem l'estat dels registres del processador perquè
   ;les funcions de C no mantenen l'estat dels registres.
   
   ; guardar r12 i r13
   push r12
   push r13

   ; els paràmtres són rdi = marks, rsi = row, rdx = col ,rcx = mines i r8b = state
   ; llegixo marks[row][col] i 
   
   ; eax = row
   mov eax, esi
   ; eax = row * 9
   imul eax, eax, 9
   ; eax = row*9 + col
   add eax, edx
   ; r9 = &marks
   mov r9, rdi
   ; r9 = &marks[row][col]
   add r9, rax

   mov bl, byte [r9]

   ; fins que sigui ' '
   cmp bl, ' '
   jne sm_end_return

   ; llegir mines[row][col]
   mov eax, esi
   imul eax, eax, 9
   add eax, edx
   ; r10 = &mines
   mov r10, rcx
   add r10, rax

   ; bl = mines[row][col]
   mov bl, byte [r10]

   ; explota si es diferent de ' '
   cmp bl, ' '
   jne sm_boom

   ; no hi ha mina, veiem veines
   xor r11d, r11d
    
   ; amunt
   cmp esi, 0
   jle validem_centre

   ; row-1 = r12d
   mov r12d, esi
   dec r12d

   ; amunt-esquerra
   cmp edx, 0
   jle saltem_amunt_esquerra
   mov r13d, edx
   dec r13d
   mov eax, r12d
   imul eax, eax, 9
   add eax, r13d
   mov bl, byte [rcx+rax]
   cmp bl, '*'
   jne saltem_amunt_esquerra
   inc r11d
saltem_amunt_esquerra:

   ; amunt-centre
   mov eax, r12d
   imul eax, eax, 9
   add eax, edx
   mov bl, byte [rcx+rax]
   cmp bl, '*'
   jne saltem_amunt_centre
   inc r11d
saltem_amunt_centre:

   ; amunt-dreta
   cmp edx, 8
   jge saltem_amunt_dreta
   mov r13d, edx
   inc r13d
   mov eax, r12d
   imul eax, eax, 9
   add eax, r13d
   mov bl, byte [rcx+rax]
   cmp bl, '*'
   jne saltem_amunt_dreta
   inc r11d
saltem_amunt_dreta:

validem_centre:

   ; centre_esquerra
   cmp edx, 0
   jle saltem_centre_esquerra
   mov r13d, edx
   dec r13d
   mov eax, esi
   imul eax, eax, 9
   add eax, r13d
   mov bl, byte [rcx+rax]
   cmp bl, '*'
   jne saltem_centre_esquerra
   inc r11d
saltem_centre_esquerra:

   ; centre_dreta
   cmp edx, 8
   jge saltem_centre_dreta
   mov r13d, edx
   inc r13d
   mov eax, esi
   imul eax, eax, 9
   add eax, r13d
   mov bl, byte [rcx+rax]
   cmp bl, '*'
   jne saltem_centre_dreta
   inc r11d
saltem_centre_dreta:

   ; avall (row < 8)
   cmp esi, 8
   jge sm_escriure_valor

   mov r12d, esi
   inc r12d

   ; avall_esquerra
   cmp edx, 0
   jle saltem_avall_esquerra
   mov r13d, edx
   dec r13d
   mov eax, r12d
   imul eax, eax, 9
   add eax, r13d
   mov bl, byte [rcx+rax]
   cmp bl, '*'
   jne saltem_avall_esquerra
   inc r11d
saltem_avall_esquerra:

   ; avall_centre
   mov eax, r12d
   imul eax, eax, 9
   add eax, edx
   mov bl, byte [rcx+rax]
   cmp bl, '*'
   jne saltem_avall_centre
   inc r11d
saltem_avall_centre:

   ; avall_dreta
   cmp edx, 8
   jge saltem_avall_dreta
   mov r13d, edx
   inc r13d
   mov eax, r12d
   imul eax, eax, 9
   add eax, r13d
   mov bl, byte [rcx+rax]
   cmp bl, '*'
   jne saltem_avall_dreta
   inc r11d
saltem_avall_dreta:

sm_escriure_valor:
   ; marks[row][col] = neighbors + '0'
   mov eax, r11d
   add al, '0'
   mov byte [r9], al

   ; cridem showMarkP2
   call showMarkP2

   mov al, r8b
   jmp sm_end

sm_boom:
   mov al, 3
   jmp sm_end

sm_end_return:
   mov al, r8b

sm_end:
   ;restaurar l'estat dels registres que s'han guardat a la pila.
   ; restaurar r12 i r13
   pop r13
   pop r12

   mov rsp, rbp
   pop rbp
   ret

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 
; Verificar si hem marcat totes les mines (numMines=0) i hem obert o
; marcat amb una mina a totes les altres caselles i no hi ha cap espai 
; en blanc (' ') a la matriu (marks), si és així, canviar l'estat 
; (state) a 2 "Guanya la partida".
; Retornar l'estat del joc actualitzat (state).
; 
; Variables globals utilitzades:	
; Cap
; 
; Paràmetres d'entrada : 
; (marks)   :rdi(rdi): Adreça de la matriu amb les mines marcades i les mines de les caselles obertes.
; (numMines):rsi(rsi): Mines que queden per marcar.
; (state)   :rdx(dx) : Estat del joc. 
; 
; Paràmetres de sortida: 
; (state)   :rax(ax) : Estat del joc.  
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;  
checkEndP2:
   push rbp
   mov  rbp, rsp
   ;guardem l'estat dels registres del processador perquè
   ;les funcions de C no mantenen l'estat dels registres.
   
   ; parametres rdi = marks, rsi = numMines i dx = state
   
   ; numMines != 0 llavors retorno state
   cmp rsi, 0
   jne che_return_state

   ; comtador
   xor r8d, r8d
   ; i = 0
   xor r9d, r9d

che_loop_i:
   cmp r9d, 9
   jge che_check_result

   ; j = 0
   xor r10d, r10d

che_loop_j:
   cmp r10d, 9
   jge che_next_i

   ; offset = i * 9 + j
   mov eax, r9d
   imul eax, eax, 9
   add eax, r10d

   ; carregar marks[i][j]
   mov bl, byte [rdi + rax]
   cmp bl, ' '
   jne che_skip_add

   ; notOpenMarks++
   inc r8d

che_skip_add:
   ; j++
   inc r10d
   jmp che_loop_j

che_next_i:
   ; i++
   inc r9d
   jmp che_loop_i

che_check_result:
   cmp r8d, 0
   jne che_return_state

   ; notOpenMarks == 0
   mov dx, 2

; retorno state
che_return_state:
   mov ax, dx

che_end:
   ;restaurar l'estat dels registres que s'han guardat a la pila. 
   mov rsp, rbp
   pop rbp
   ret
   
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; Joc del Buscamines
; Subrutina principal del joc
; Permet jugar al joc del buscamines cridant totes les funcionalitats.
;
;Pseudo codi:
; Mostrar el tauler de joc (cridar la funció PrintBoardP2_C).
; Comptar el nombre de mines (numMines) que hi ha la matriu (mines) 
; cridant la subrutina countMinesP2.
; Marcar una mina a la fila (row)=8, columna (col)=8 
; cridant a la subrutina markMineP2.
; Mostrar el nombre de mines (numMines) cridant la subrutina showNumMinesP2.
; 
; Inicialitzar l'estat del joc, (state=1) per començar a jugar.
; Fixar la posició inicial del cursor a la fila (row)=5, columna (col)=4.
; 
; Mentre (state=1) fer:
;   Posicionar el cursor a la pantalla dins del tauler, en funció de
;   la fila (row) i la columna(col) cridant la subrutina posCursorP2.
;   Llegir una tecla i guardar-la a la variable local (charac) cridant
;     a la subrutina getchP2.  
;   Segons la tecla llegida cridarem a la subrutina corresponent.
;     - ['i','j','k' o 'l']  cridar a la subrutina moveCursorP2 i 
;                            actualitzar la fila (row) i la columa (col)
;                            a partir del valor retornat (indexMat).
;     - 'm'                  cridar a la subrutina markMineP2 i 
;                            la subrutina showNumMinesP2 per actualitzar
;                            el valor de (numMines) al tauler.
;     - '<espace>'(codi ASCII 32) cridar a la subrutina searchMinesP2.
;     - '<ESC>'   (codi ASCII 27) posar (state = 0) per a sortir.   
;   Verificar si hem marcat totes les mines i si hem obert totes  
;   les caselles cridant a la subrutina checkEndP2_C.
; Fi mentre.
; Sortir:
; Si s'ha obert una mina (state==3) mostrar totes les mines de 
; la matriu (mines) cridant la subrutina showMarkP2.
; Mostrar el missatge de sortida que correspongui cridant a la funció
; printMessageP2_C.
; S'acabat el joc.
; 
; Variables globals utilitzades:
; Cap
; 
; Paràmetres d'entrada : 
; (marks) :rdi(rdi): Adreça de la matriu amb les mines marcades i les mines de les caselles obertes.
; (mines) :rsi(rsi): Adreça de la matriu amb les mines.
; 
; Paràmetres de sortida: 
; Cap.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
playP2:
   push rbp
   mov  rbp, rsp
   ;guardem l'estat dels registres del processador perquè
   ;les funcions de C no mantenen l'estat dels registres.
   push rax
   push rbx
   push rcx
   push rdx
   push rdi
   push rsi
   push r8
   push r9
   push r10
   push r11
   push r12
   push r13
   push r14
   
   push rdi
   push rsi
   call printBoardP2_C   ;printBoard2_C();
   pop  rsi
   pop  rdi
   
   mov  r12, rdi         ;marks
   mov  r13, rsi         ;mines
   mov  rdi, r13
   call countMinesP2     ;numMines = countMinesP2_C(mines);
   mov  r14,  rax
   mov  r10d, 8          ;row=8; 
   mov  r11d, 8          ;col=8;
   mov  rdi, r12
   mov  esi, r10d
   mov  edx, r11d
   mov  rcx, r14
   call markMineP2       ;numMines = markMineP2_C(marks, row, col, numMines);
   mov  r14, rax
   mov  r9w, 1          ;state = 1;
   mov  r10d, 5          ;row=5;
   mov  r11d, 4          ;col=4;

   p_while               ;bucle principal del joc.
   cmp  r9w, 1          ;while (state == 1) {            
   jne  p_printMessage

   mov  edi, r10d        ;row
   mov  esi, r11d        ;col
   call posCursorP2      ;posCursorP2_C(row, col); 

   call getchP2          ;(al)charac = getchP2_C();

   p_move:
   cmp al, 'i'		     ;if (charac>='i'
   jl  p_mark
   cmp al, 'l'		     ;&& charac<='l')
   jle p_moveCursor
   p_mark:
   cmp al, 'm'		     ;if (charac=='m') { 
   je  p_mineMarker
   p_open:
   cmp al, ' '           ;if (charac==' ') {
   je  p_searchMines
   p_esc:
   cmp al, 27		     ;if (charac==27) {  
   je  p_exit
   jmp p_check  
    
   p_moveCursor:
   mov  dil, al          ;charac
   mov  esi, r10d        ;row
   mov  edx, r11d        ;col
   call moveCursorP2     ;indexMat = moveCursorP2_C(charac, row, col);
   mov  edx, 0
   mov  ebx, DIMMATRIX          
   div  ebx              ;EAX:row=(posCursor/DIMMATRIX) EDX:col=(posCursor%DIMMATRIX)
   mov  r10d, eax
   mov  r11d, edx
   jmp  p_check

   p_mineMarker:
   mov  rdi, r12         ;marks
   mov  esi, r10d        ;row
   mov  edx, r11d        ;col
   mov  rcx, r14         ;numMines
   call markMineP2       ;numMines = markMineP2_C(marks, row, col, numMines);
   mov  r14, rax
   mov  rdi, r14
   call showNumMinesP2   ;showNumMinesP2_C(numMines);
   jmp  p_check

   p_searchMines:
   mov  rdi, r12         ;marks
   mov  esi, r10d        ;row
   mov  edx, r11d        ;col
   mov  rcx, r13         ;mines
   mov  r8w, r9w         ;state
   call searchMinesP2    ;state = searchMinesP2_C(marks, row, col, mines, state);
   mov  r9w, ax
   jmp  p_check

   p_exit:
   mov  r9w, 0           ;state = 0;
 
   p_check:
   mov  rdi, r12         ;marks
   mov  rsi, r14         ;numMines
   mov  dx,  r9w         ;state
   call checkEndP2       ;state = checkEndP2_C(marks, numMines, state);
   mov  r9w, ax
   
   jmp  p_while

   p_printMessage:
       
   cmp r9w, 3             ;if(state==3){
   jne p_endif1
     mov r10b, 0          ;row=0;
     p_for1:             
	 cmp r10b, DIMMATRIX  ;for (row=0;row<DIMMATRIX;row++){
	 jge p_endfor1
	   mov r11b, 0        ;col=0;
	   p_for2:
	   cmp r11b, DIMMATRIX;for (col=0;col<DIMMATRIX;col++){
	   jge p_endfor2 
         mov  rdi, r13     ;mines
         mov  esi, r10d    ;row
         mov  edx, r11d    ;col   
         call showMarkP2   ;showMarkP2_C(mines,row, col);
         inc r11b          ;col++
         jmp p_for2
       p_endfor2:            ;}
       inc r10b              ;row++
       jmp p_for1
     p_endfor1:
   p_endif1:               ;}

   mov  di, r9w
   call printMessageP2_C ;printMessageP2_C(state);
      
   p_end:
   ;restaurar l'estat dels registres que s'han guardat a la pila.
   pop r14
   pop r13
   pop r12
   pop r11
   pop r10
   pop r9
   pop r8
   pop rdi
   pop rsi
   pop rdx
   pop rcx
   pop rbx
   pop rax
   
   mov rsp, rbp
   pop rbp
   ret
